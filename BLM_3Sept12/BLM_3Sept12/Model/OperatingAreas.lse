// Landscape event definition for creating operating areas for the Vermillon landscape model
LSEVENT: OPERATING_LSE

DEFINITIONS
   GLOBAL VARIABLE: OASizeHa, OATimespan, NumActiveOAs, BaseTimestep
   GLOBAL CONSTANT: HaPerCell, MaxActiveOAs

   LAYER: OperatingArea, StudyArea
   CLUSTER VARIABLE: currOASize, startLocation, OAId
   CELL VARIABLE: d
   EVENT VARIABLE: nextOAId, NumToRetire, nToRetire, YearofLastUpdate
   LOCAL: residual
   CONSTANT: startTime = 0.73
ENDDEF

INITIALSTATE
   INITIALSTATE = 1
   residual = 0
ENDIS

RETURNTIME
   NumActiveOAs = CLAMP(NumActiveOAs, 0, MaxActiveOAs)
   OATimespan = MAX(1,OATimespan)
   RETURNTIME = IF (Time EQ 0) THEN startTime 
                ELSE MAX(BaseTimestep,FLOOR(OATimespan/MAX(NumActiveOAs,1)))

   Year = FLOOR(Time)
   YearsSinceUpdate = IF (Year EQ 0) THEN 0 ELSE Year - YearofLastUpdate
   YearofLastUpdate = Year

   expectedNumToRetire = NumActiveOAs/OATimespan * YearsSinceUpdate
   NumToRetire = FLOOR(expectedNumToRetire)
   nToRetire = MAX(1,NumToRetire)

   residual = residual + (expectedNumToRetire - NumToRetire)
   IF residual >= 1
      NumToRetire = NumToRetire + 1
      residual  = residual + 1
   END
//   residual = (residual - FLOOR(residual)) + CEIL(OATimespan/MAX(NumActiveOAs,1)) - (OATimespan/MAX(NumActiveOAs,1))
ENDRT

EVENTLOCATION
  STATIC REGION WHOLE MAP
     DECISION StudyArea > 0

  OperatingArea = MAX(0,OperatingArea-nToRetire)
  nextOAId = NumActiveOAs
ENDEL

NUMCLUSTERS = IF (Time EQ startTime) THEN NumActiveOAs ELSE CEIL(NumActiveOAs/OATimespan)

PROBINIT
   PROBINIT = (OperatingArea EQ 0)
   currOASize = 0
   startLocation = Location
   d = 0
   OAId = nextOAId
   nextOAId = MAX(0,nextOAId - 1)
ENDPI

TRANSITIONS
   TRANSITIONS = (currOASize < OASizeHa)

   OperatingArea = OAId
   currOASize = currOASize + HaPerCell
ENDTR

SPREADTIME = 0.0000000001 * d

SPREADLOCATION
   REGION CENTRED(1,1)
      DECISION (StudyArea > 0) AND (OperatingArea EQ 0)
ENDSL

SPREADPROB
   SPREADPROB = 1

   d = DISTANCE(Location,startLocation) - DISTANCE(SOURCE Location,startLocation)
ENDSP
