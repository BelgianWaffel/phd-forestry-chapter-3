// This event is designed to help explore initiation
// Initiation involves interactions among EventLocation, ProbInit and NumClusters
LSEVENT: Initiation

DEFINITIONS
   LAYER: EventLoc, ProbInitSurface, Initiations
   GLOBAL VARIABLE: currClusterId
   GLOBAL CONSTANT: MaxInstances, MaxProbInit

   LAYER: StudyArea, Elevation, Spp1
ENDDEF



RETURNTIME = 1

// ----------------------  EVENT LOCATION  ---------------------------
// The EventLocation property specifies the set of cells in which the
// event can potentially initiate.  This property can be used to
// restrict the occurrence of an event to a particular spatial region,
// or to a particular type of landscape element.
// If not specified, by default is every cell in the whole map.
// -------------------------------------------------------------------
EVENTLOCATION
   // Preliminary assignment expressions are evaluated globally (event instance
   // context) prior to evaluating the main expression.

   // Main expression must be a region
   EVENTLOCATION = REGION WHOLE MAP
                   DECISION (StudyArea > 0) //AND (Spp1 > 0)

   // Consequent assignment expressions are evaluated in each cell    //
   // selected by EventLocation as a potential initiation site, prior //
   // to evaluating NumClusters and ProbInit.                       //
   EventLoc = MIN(EventId, MaxInstances)
ENDEL


// ------------------------- NUM CLUSTERS -----------------------------
// The NumClusters property specifies the number of cells in which the 
// current event instance will  initiate.  This property "selects" a sub-set
// of the cells identified as initiating cells by ProbInit.
// This property works in conjucntion with ProbInit (as described below),
// see the SELES programmers manual for a complete description.
// If not specified, by default the number of initiating cells will be
// detemined soley by ProbInit.
// ---------------------------------------------------------------------
NUMCLUSTERS
   // Preliminary assignment expressions are evaluated globally
   // prior to evaluating NumClusters

   // Main expression must evaluate to numeric value
   NUMCLUSTERS = 1000//-1 // negative means "undefined" (same as if it wasn't specified)

   // Consequent assignment expressions are evaluated in each cell
   // in which the event actually initiates, prior to evaluating the
   // consequence assignments of ProbInit and performing Transitions.
   currClusterId = currClusterId + 1
ENDNC


// -----------------------  PROB INIT  -------------------------------
// The ProbInit property specifies the probability that the event
// initiates in a particular cell. If NumClusters is not specified,
// ProbInit yields an absolute probability for each cell, and the
// actual initiating cells are selected using this probability.
// If NumClusters is specified, ProbInit yields a relative
// probability that influences which cells are selected for initiation.
// This property provides an opportunity to use the existing landscape
// pattern (spatial raster layers) to influence  the spatial behaviour
// of the process
// If not specified, by default each cell has an equal probability of
// initiation (i.e., ProbInit = 1; or the landscape is homogeneous
// with respect to the occurrence of the event).
// -------------------------------------------------------------------
PROBINIT
   // Preliminary assignment expressions are evaluated in each cell
   // defined by the EventLocation, prior to evaluating ProbInit.
   p = 100*((Elevation-600)/1000)^2
   ProbInitSurface = CLAMP(p,0,MaxProbInit)

   // Main expression must evaluate to numeric value.
   PROBINIT = p //(p/100) // 10 //0.01 //

   // Consequent assignment expressions are evaluated in each cell
   // in which the event actually initiates, prior to transitions.
   Initiations = MIN(EventId, MaxInstances)
ENDPI

