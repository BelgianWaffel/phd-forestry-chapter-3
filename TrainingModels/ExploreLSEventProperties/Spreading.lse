LSEVENT: SpreadingTypes

DEFINITIONS
   GLOBAL CONSTANT: MaxDist
   LAYER: TimeFromSrc, DistanceFromSrc

   // Spread types:
   //  1: spread to 4 cardinal adjacent neighbours at a time step of 1
   //  2: spread to 8 adjacent neighbours at a time step of 1
   //  3: spread to 8 adjacent neighbour at a time step equal to distance (1 for cardinal neighbours, and root(2) for diagonal
   //  4: on even time steps: spread to 4 adjacent cardinal neighbours, 
   //     on odd time steps spread to 8 adjacent neighbours, all at a time step of 1
   //  5: spread to 8 adjacent neighours at a time step equal to the incremental distance from the start
   GLOBAL VARIABLE: SpreadType, nActiveCells

   CLUSTER VARIABLE: StartLocation
   CELL VARIABLE: distInc, d
ENDDEF

RETURNTIME
   SpreadType = CLAMP(SpreadType,1,5) // don't allow setting outside range 1-5
   RETURNTIME = 0
ENDRT

// Start in the centre cell
EVENTLOCATION
   centreLoc = LOCATION(NUMROWS/2, NUMCOLS/2)
   REGION LOCATION(centreLoc)
ENDEL

PROBINIT
   PROBINIT = 1

   StartLocation = Location
   distInc = 0
   d = 0
ENDPI

TRANSITIONS
   TRANSITIONS = TRUE

   nActiveCells++
   DistanceFromSrc = ROUND(DISTANCE(StartLocation, Location))
ENDTR

SPREADTIME
   SPREADTIME = distInc
   TimeFromSrc = ROUND(Time)
   nActiveCells--
ENDST

SPREADLOCATION
   maxD  = CLASSIFY(SpreadType)
               1: 1
               2: 1.5
               3: 1.5
               4: IF (Time % 2) EQ 0 THEN 1 ELSE 1.5
               5: 1.5
              END
   
   REGION CENTRED(1, maxD)
      DECISION DistanceFromSrc EQ MaxDist
ENDSL


SPREADPROB
   SPREADPROB = 1

   d = DISTANCE(StartLocation, Location)

   distInc = CLASSIFY(SpreadType)
               1: 1
               2: 1
               3: DISTANCE(Location, SOURCE Location)
               4: 1
               5: MAX(0,d - SOURCE d)
              END
ENDSP
