// Landscape event definition for succession (simple aging v2) in the case study model
LSEVENT: Aging

DEFINITIONS
   GLOBAL CONSTANT: MaxStandAge

   // Parameters
   GLOBAL VARIABLE: BaseTimestep   // Base Time step (in years)

   // Static layers
   LAYER: StudyArea

   // Dynamic layers
   LAYER: StandAge, OldForest, Spp1

   // Tracking
   GLOBAL VARIABLE: AreaOldForest
ENDDEF

INITIALSTATE
   // Set up the initial state
   OVER REGION WHOLE MAP
      DECISION (StudyArea > 0) AND (Spp1 > 0)

      // Old forest is defined as 250 years or older
      OldForest = (StandAge >= 250)
   END

   INITIALSTATE = 1  // create one instance on startup (default)
ENDIS


// Return each time step
// Aging is at start of year (before any other events)
RETURNTIME
   RETURNTIME = BaseTimestep

   // Reset area of old forest at start of each step
   AreaOldForest = 0
ENDRT

// Only process forested cells (the "STATIC" keyword means: compute region once on startup and should only be use when the decision is static)
EVENTLOCATION
   STATIC REGION WHOLE MAP
      DECISION (StudyArea > 0) AND (Spp1 > 0)
ENDEL


// In each forested cell
TRANSITIONS
   // Increment the stand age in years (up to the maximum age)
   StandAge = MIN(StandAge + BaseTimestep, MaxStandAge)

   // Old forest is over 250 years
   OldForest = (StandAge >= 250)

   // Sum area of old forest
   AreaOldForest = AreaOldForest + OldForest

   TRANSITIONS = TRUE
ENDTR

