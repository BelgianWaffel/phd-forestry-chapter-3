// Landscape event definition for the third version of the Goshawk HSI for the case study model
LSEVENT: GoshawkHSI

DEFINITIONS
   GLOBAL CONSTANT: HaPerCell, CellWidth

   // Parameters
   GLOBAL VARIABLE: BaseTimestep   // Base Time step (in years)

   // Input (static) layers
   LAYER: StudyArea

   // Dynamic layers (but used as input to the HSI)
   LAYER: StandAge, Spp1

   // Input tables (species weights array)
   GLOBAL CONSTANT: SpeciesRatings[]

   // output layers
   LAYER: NestAreaHSI, HardEdge, NestAreaHSIClass
   GLOBAL VARIABLE: TotHSIbyClass[]
ENDDEF

RETURNTIME
   RETURNTIME = BaseTimestep // Return each time step

   TotHSIbyClass [=] 0 // reset entire array using a matrix assignment "[=]"
ENDRT


// Initiate in all cells in the study area with a lead species
// (the "STATIC" keyword means: compute region once on startup and should only be use when the decision is static)
EVENTLOCATION
   STATIC REGION WHOLE MAP
      DECISION (StudyArea > 0) AND (Spp1 > 0)
ENDEL


// Compute HSI v3
TRANSITIONS
   TRANSITIONS = TRUE

   // Nest Area HSI v3 combines ratings based on species, age and edge
   SppRating = SpeciesRatings[MAX(0,Spp1)]

   AgeRating = IF StandAge <= 60 THEN 0
               ELSE IF StandAge <= 80  THEN 0.1
               ELSE IF StandAge <= 100  THEN 0.3
               ELSE IF StandAge <= 120  THEN 0.5
               ELSE IF StandAge <= 140 THEN 0.8
               ELSE IF StandAge <= 250 THEN 1
               ELSE 0.8

   // Hard edges are defined as forest that is at least 10 m tall and that share a boundary with at least one adjacent heighbour that is
   // are either non-forest or forest < 20 m tall that is > 10m shorter than the focal cell
   // For projection, thi smodel uses StandAge/2 as a surrogate for height (assumes 0.5 m height growht / year)
   hSurrogate = (StandAge/2)
   IF (hSurrogate >= 10)
      nEdge = 0

      // Note: the expressions within the OVER REGION are in the spatial context of the adjacent neighbour
      OVER REGION CENTRED(1,1) // visit the 4 adjacent cardinal neighbours (diagonals aren't considered to share a significant boundary)
         DECISION StudyArea > 0

         hNeighbSurrogate = (StandAge/2) // hNeighSurrogate is the height surrogate of the current neighbour in metres

         // A neighbour creates a hard edge if it is non-forest (no Spp1) or < 20 m tall and > 10 m shorter than the focal cell
         IF (Spp1 <= 0) OR ((Spp1 > 0) AND (hNeighbSurrogate < 20) AND ((hSurrogate - hNeighbSurrogate) > 10))
            nEdge = nEdge + 1
         END
      END

      // Hard edge: at least one adjacent neighobur that creates a hard edge
      HardEdge = nEdge > 0

   ELSE // < 10 m tall: not a hard edge (only applies to forest >= 10 m)
      HardEdge = 0
   END

   EdgeRating = IF HardEdge THEN 0.7 ELSE 1

   // Calculate HSI and HSI class
   NestAreaHSI = CLAMP(100 * SppRating * AgeRating * EdgeRating,0,100)
   NestAreaHSIClass = IF NestAreaHSI < 25 THEN 0
                      ELSE IF NestAreaHSI < 50 THEN 1
                      ELSE IF NestAreaHSI < 75 THEN 2
                      ELSE 3
   TotHSIbyClass[NestAreaHSIClass] = TotHSIbyClass[NestAreaHSIClass] + HaPerCell
ENDTR

