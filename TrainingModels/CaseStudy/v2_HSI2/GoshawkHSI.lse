// Landscape event definition for the second version of the Goshawk HSI for the case study model
LSEVENT: GoshawkHSI

DEFINITIONS
   GLOBAL CONSTANT: HaPerCell, CellWidth

   // Input (static) layers
   // Note: StandHeight10 is in decimeters (or 1/10 of a metre)
   LAYER: StudyArea, StandAge, StandHeight10, CrownClosureClass, Spp1

   // Input tables (species weights array)
   GLOBAL CONSTANT: SpeciesRatings[]

   // output layers
   LAYER: NestAreaHSI, HardEdge, NestAreaHSIClass
ENDDEF


RETURNTIME = 0 // Run once at time zero, then stop


// Initiate in all cells in the study area with a lead species
EVENTLOCATION
   REGION WHOLE MAP
      DECISION (StudyArea > 0) AND (Spp1 > 0)
ENDEL


// Compute HSI v2
TRANSITIONS
   TRANSITIONS = TRUE

   h = StandHeight10/10 // h is in metres

   // The nest Area HSI v2 combines ratings based on species, age, height, crown closure and edge
   SppRating = SpeciesRatings[MAX(0,Spp1)]

   AgeRating = IF StandAge <= 60 THEN 0
               ELSE IF StandAge <= 80  THEN 0.1
               ELSE IF StandAge <= 100  THEN 0.3
               ELSE IF StandAge <= 120  THEN 0.5
               ELSE IF StandAge <= 140 THEN 0.8
               ELSE IF StandAge <= 250 THEN 1
               ELSE 0.8

   HeightRating = IF h <= 3 THEN 0
                  ELSE IF h < 9 THEN MAX(0.1,(h-3) * 0.016667)
                  ELSE IF h < 20 THEN MAX(1,0.1 + (h-9) * 0.0818182)
                  ELSE 1

   CCRating = IF CrownClosureClass <= 2 THEN 0
              ELSE IF CrownClosureClass <= 3 THEN 0.3
              ELSE IF CrownClosureClass <= 4 THEN 0.6
              ELSE IF CrownClosureClass <= 7 THEN 1
              ELSE 0.8

   // Hard edges are defined as forest that is at least 10 m tall and that share a boundary with at least one adjacent heighbour that is
   // are either non-forest or forest < 20 m tall that is > 10m shorter than the focal cell
   IF (h >= 10)
      nEdge = 0

      // Note: the expressions within the OVER REGION are in the spatial context of the adjacent neighbour
      OVER REGION CENTRED(1,1) // visit the 4 adjacent cardinal neighbours (diagonals aren't considered to share a significant boundary)
         DECISION StudyArea > 0

         hNeighb = StandHeight10/10 // hNeighb is the height of the current neighbour in metres

         // A neighbour creates a hard edge if it is non-forest (no Spp1) or < 20 m tall and > 10 m shorter than the focal cell
         IF (Spp1 <= 0) OR ((Spp1 > 0) AND (hNeighb < 20) AND ((h - hNeighb) > 10))
            nEdge = nEdge + 1
         END
      END

      // Hard edge: at least one adjacent neighobur that creates a hard edge
      HardEdge = nEdge > 0

   ELSE // < 10 m tall: not a hard edge (only applies to forest >= 10 m)
      HardEdge = 0
   END

   EdgeRating = IF HardEdge THEN 0.7 ELSE 1

   // Calculate HSI and HSI class
   NestAreaHSI = CLAMP(100 * SppRating * CCRating * ((AgeRating + HeightRating)/2) * EdgeRating,0,100)
   NestAreaHSIClass = IF NestAreaHSI < 25 THEN 0
                      ELSE IF NestAreaHSI < 50 THEN 1
                      ELSE IF NestAreaHSI < 75 THEN 2
                      ELSE 3
ENDTR

