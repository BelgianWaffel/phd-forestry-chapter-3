LSEVENT: SimpleFireTopDown

DEFINITIONS
   GLOBAL CONSTANT: MaxTSF
   LAYER: StudyArea, TimeSinceFire

   // Parameters
   GLOBAL VARIABLE: MeanFiresPerYear, MeanFireSize
   GLOBAL VARIABLE: sprdRelAgeExp, sprdMidComplexShp

   // Inputs
   GLOBAL VARIABLE: ForestSize

    // Outputs
   GLOBAL VARIABLE: AreaBurned, FireCycle

   // Cluster variables (one for each fire)
   CLUSTER VARIABLE: Extent
ENDDEF


RETURNTIME
   RETURNTIME = 1 // each step

   // Calculate fire cycle (MeanFiresPerYear and MeanFireSize are parameters)
   FireCycle = ForestSize / (MeanFireSize * MeanFiresPerYear)
ENDRT


// Potentially initiate in every cell in the study area (since NumClusters is set, only some of these will be selected)
EVENTLOCATION
   REGION WHOLE MAP
   ? StudyArea > 0
ENDEL


// Pick number of fires with random ignition (from a negative exponential distribution)
NUMCLUSTERS
   NUMCLUSTERS = ROUND(NEGEXP(MeanFiresPerYear))

   // For each fire, pick a size
   Extent = ROUND(NEGEXP(MeanFireSize))
ENDNC


TRANSITIONS
   // Make a transition only if there is still extent to be burned (AND if the stand didn't already burn during this time step)
   // Top-down: spread until selected extent burned
   TRANSITIONS = (Extent >= 1) AND (TimeSinceFire > 0)

   // Set the time since fire to 0 and increment area burned
   TimeSinceFire = 0
   AreaBurned = AreaBurned + 1

  // Decrement the number of cells remaining to burn for this opening
  Extent = Extent - 1
ENDTR


// Spread rate::1 milliStep / spread iteration (want to ensure fire goes out before next step)
SPREADTIME = 1/1000


// Allow spread to the 4 cardinal neighbours: up, down, left and right
SPREADLOCATION
   REGION CENTRED(1,1)
     DECISION TimeSinceFire > 0
ENDSL


NUMRECIPIENTS
  // Mid-Complex shapes. This is the number of neighbours to spread to from a burning cell.
  // Lower mean gives more complex shapes. Higher standard deviation gives more variety of shapes.
  NUMRECIPIENTS = IF sprdMidComplexShp 
                  THEN CLAMP(NORMAL(0.25,1), 1,3)
                  ELSE 4 // all 4
ENDNR


SPREADPROB
   // Spread with relative probability increasing with age if sprdRelAgeExp > 0
   SPREADPROB = (TimeSinceFire/MaxTSF)^sprdRelAgeExp
ENDSP



