LSEVENT: SimpleFireBottomUp

DEFINITIONS
   LAYER: StudyArea, TimeSinceFire

   // Parameters
   GLOBAL VARIABLE: MeanFiresPerYear, probBurn

   // Inputs
   GLOBAL VARIABLE: ForestSize

    // Outputs
   GLOBAL VARIABLE: AreaBurned, FireCycle
ENDDEF


RETURNTIME
   RETURNTIME = 1 // each step

   // Calculate emergent fire cycle (MeanFireSize and MeanFiresPerYear are the parameters)
   FireCycle = IF AreaBurned < 0 THEN 0
               ELSE ForestSize / (AreaBurned / Time)
ENDRT


// Potentially initiate in every cell in the study area (since NumClusters is set, only some of these will be selected)
EVENTLOCATION
   REGION WHOLE MAP
   ? StudyArea > 0
ENDEL


// Pick number of fires with random ignition (from a negative exponential distribution)
NUMCLUSTERS
   NUMCLUSTERS = ROUND(NEGEXP(MeanFiresPerYear))
ENDNC


TRANSITIONS
   // Stochastically choose to burn or not
   burn = UNIFORM(0,1) <= probBurn

   // Make a transition only if cell burns (AND if the stand didn't already burn during this time step)
   // Bottom up: decisions made in fire front cells; fire goes out when there are no more active front cells
   TRANSITIONS = burn AND (TimeSinceFire > 0)

   // Set the time since fire to 0 and increment area burned
   TimeSinceFire = 0
   AreaBurned = AreaBurned + 1
ENDTR


// Spread rate::1 milliStep / spread iteration (want to ensure fire goes out before next step)
SPREADTIME = 1/1000


// Allow spread to the 4 cardinal neighbours: up, down, left and right
SPREADLOCATION
   REGION CENTRED(1,1)
     DECISION TimeSinceFire > 0
ENDSL


SPREADPROB = 1


